/*
 * This file uses GeckoLib, licensed under the MIT License.
 * Copyright © 2024 GeckoThePecko.
 */

buildscript {
    //Lo de acá adentro solo sirve para cosas de build.gradle y solo para el script.
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'java'
    id 'idea'
    //ForgeGradle
    id 'net.minecraftforge.gradle' version '6.0.30'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
    // Mixins
    id 'org.spongepowered.mixin' version '0.7.38'
}

version = mod_version
group = mod_group_id

base {
    archivesName = mod_id
}

java {
    withSourcesJar()
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

//Some info that gets printed in the console when the game is launched.
println "Java: ${System.getProperty 'java.version'}, " +
        "JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), " +
        "Arch: ${System.getProperty 'os.arch'}"

mixin {
    // MixinGradle Settings
    add sourceSets.main, 'mixins.dragonminez.refmap.json'
    config 'dragonminez.mixins.json'
    debug = false // Default to false

    // Enable debug only for runClient task in IntelliJ debug mode
    gradle.taskGraph.whenReady { taskGraph ->
        if (taskGraph.hasTask(':runClient') && System.getProperty('idea.active') != null) {
            debug = true
        }
    }
}

//Make the result of jarJar the default.
tasks.jarJar.configure {
    it.archiveClassifier.set("")
    finalizedBy("reobfJarJar")
}

tasks.jar.configure {
    it.archiveClassifier.set("slim")
}

//La diferencia de esto con el buildScript, es que este pone los repositorios como librerías + los de build.gradle
// + cuando se hace el jar, estas cosas se incluyen.
repositories {
    maven {
        name = 'GeckoLib'
        url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/'
        content {
            includeGroupByRegex("software\\.bernie.*")
            includeGroup("com.eliotlash.mclib")
        }
    }
    maven {
        name = "Jared's maven"
        url = "https://maven.blamejared.com/" // Integración para el JEI, NO ES DEPENDENCIA, ES COMPATIBILIDAD.
    }
    maven {
        url = "https://cursemaven.com"
    }
    mavenCentral()
}

minecraft {
    jarJar.enable()
    mappings channel: mapping_channel, version: mapping_version
    copyIdeResources = true

    runs {
        configureEach {
            workingDirectory('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'fml.earlyprogresswindow', 'false'
            mods {
                "${mod_id}" {
                    source sourceSets.main //False-positive in IntelliJ IDEA
                }
            }
        }

        client {
            property 'forge.logging.console.level', 'debug'
        }
        server {
            property 'forge.logging.console.level', 'debug'
        }

        gameTestServer {
            property 'forge.enabledGameTestNamespaces', mod_id
        }

        data {
            workingDirectory('run-data')
            args '--mod', mod_id, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
        }
    }
}

dependencies {
    // Minecraft + Mixin
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    annotationProcessor 'org.spongepowered:mixin:0.8.7:processor'

    //Se usa GeckoLib
    implementation fg.deobf('software.bernie.geckolib:geckolib-forge-1.20.1:4.7.1.2')
    implementation 'com.eliotlash.mclib:mclib:20'

    //Terrablender
    implementation fg.deobf('com.github.glitchfiend:TerraBlender-forge:1.20.1-3.0.1.7')

    //Corrección a puntos vulnerables de dependencias en Forge:
    implementation 'com.google.guava:guava:33.4.8-jre'
    implementation 'io.netty:netty-codec:4.2.0.Final'
    implementation 'io.netty:netty-handler:4.2.0.Final'
    implementation 'org.apache.commons:commons-compress:1.27.1'
    minecraftLibrary jarJar("com.fasterxml.jackson.core:jackson-databind:2.18.3"), {
        jarJar.ranged(it, "[2.18.3,)")
    }

    // Just Enough Items (JEI)
    compileOnly(fg.deobf("mezz.jei:jei-${minecraft_version}-common-api:${jei_version}"))
    compileOnly(fg.deobf("mezz.jei:jei-${minecraft_version}-forge-api:${jei_version}"))
    compileOnly(fg.deobf("org.embeddedt:embeddium-1.20.1:0.3.9-git.f603a93+mc1.20.1"))
    // Este es solo para el runClient, no es necesario para el jar
    runtimeOnly(fg.deobf("mezz.jei:jei-${minecraft_version}-forge:${jei_version}"))
    runtimeOnly(fg.deobf("org.embeddedt:embeddium-1.20.1:0.3.9-git.f603a93+mc1.20.1"))

    // Mods (Solo en runClient) que aportan cosas útiles para el desarrollo.
    runtimeOnly(fg.deobf("curse.maven:worldedit-225608:4586218")) // WorldEdit, nada más que decir xd
    runtimeOnly(fg.deobf("curse.maven:cyanide-541676:5778405")) // Cyanide es un mod para saber exactamente qué y dónde está fallando un archivo Json (Muy util en worldgen)
}

sourceSets.main.resources { srcDir 'src/generated/resources/' }

tasks.named('processResources', ProcessResources) {
    var replaceProperties = [
            minecraft_version         : minecraft_version, minecraft_version_range: minecraft_version_range,
            forge_version             : forge_version, forge_version_range: forge_version_range,
            loader_version_range      : loader_version_range,
            mod_id                    : mod_id, mod_name: mod_name, mod_license: mod_license, mod_version: mod_version,
            mod_authors               : mod_authors, mod_description: mod_description,
            geckolib_version_range    : geckolib_version_range,
            terrablender_version_range: terrablender_version_range
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
        expand replaceProperties + [project: project]
    }
}

jar {
    manifest {
        attributes([
                'Specification-Title'     : mod_id,
                'Specification-Vendor'    : mod_authors,
                'Specification-Version'   : mod_version,
                'Implementation-Title'    : project.name,
                'Implementation-Version'  : project.version,
                'Implementation-Vendor'   : mod_authors,
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
    finalizedBy 'reobfJar'
}